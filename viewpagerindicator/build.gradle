apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

version = '1.0.0'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 25
        versionCode 1
        versionName "1.0.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:25.1.1'
    testCompile 'junit:junit:4.12'
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}

group = 'com.luciferldy.viewpagerindicator'
install {
    repositories.mavenInstaller {
        pom.project {
            packaging 'aar'
            groupId 'com.luciferldy.viewpagerindicator' //自己定义的组名
            artifactId 'viewpagerindicator'

            name 'ViewPagerIndicaotr'
            description 'A simple indicator library for Android ViewPager.'
            url 'https://github.com/luciferldy/ViewPagerIndicator'
            inceptionYear '2017'

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            scm {
                connection 'https://github.com/luciferldy/ViewPagerIndicator.git'
                url 'https://github.com/luciferldy/ViewPagerIndicator'

            }
            developers {
                developer {
                    name 'luciferldy'
                    email 'lian_dy@foxmail.com'
                }
            }
        }

    }
}

// Bintray

//获取local.propertes的信息
Properties properties = new Properties()
properties.load(project.rootProject.file('info.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    publish = true
    configurations = ['archives']
    pkg {
        //填入 bintray 上对应的 package 信息
        repo = 'maven'
        name = 'ViewPagerIndicator'
        vcsUrl = 'https://github.com/luciferldy/ViewPagerIndicator.git'
        websiteUrl = 'https://github.com/luciferldy/ViewPagerIndicator'
        licenses = ['Apache-2.0']
        issueTrackerUrl = 'https://github.com/luciferldy/ViewPagerIndicator/issues'
        publicDownloadNumbers = true
        version {
            name = '1.0.0'
            desc = 'A simple indicator library for Android ViewPager.'
            vcsTag = '1.0.0'
            attributes = ['gradle-plugin': 'com.use.less:com.use.less.gradle:gradle-useless-plugin']
        }
    }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

task findConventions << {
    println project.getConvention()
}
